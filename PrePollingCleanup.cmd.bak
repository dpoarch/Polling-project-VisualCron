::------------------------------------------------------------------------------
:: PrePollingCleanup.cmd
::
:: Created by: Christian Adams
:: Date: 7/19/2002
::
:: Description:
::     This program will perform pre-polling cleanup tasks.
::
:: Usage: 
::     PrePollingCleanup
::
:: Dependent programs:
::     MoveFiles.exe (Christian Adams) - Moves oneor more files.
::
::     Now.exe (NT Resource Kit) - Displays the current date and time.
::
::     Blat.exe - Sends SMTP email.
::
:: Environment variables:
::     ERROR_FLAG - used for flagging fatal errors.
::
::     WARNING_FLAG - used for flagging non-fatal errors.
::
::     PROG_FOLDER - folder containing this program.
::
::     LOG_FILE - path and filename of the log file.
::
::     ERR_FILE - path and filename of the error file.
::
::------------------------------------------------------------------------------

  :: Set variables.
  @set ERROR_FLAG=FALSE
  @set WARNING_FLAG=FALSE
  @set PROG_FOLDER=E:\Poll\Bin
  @set LOG_FILE=%PROG_FOLDER%\Logs\PrePollingCleanup.log
  @set ERR_FILE=%PROG_FOLDER%\Logs\PrePollingCleanup.err

  :: Set window title and color
  @Title Pre-polling Cleanup
  @Color 0E

  :: Create new log file.
  @echo. > %LOG_FILE%
  @call :MESS Begin Program

  @cls
  @echo *******************************
  @echo * PrePollingCleanup.cmd       *
  @echo *                             *
  @echo * Created by: Christian Adams *
  @echo *******************************

  @echo.
  @echo Cleanup folders used during nightly polling.
  @echo.
  @pause

:MOVETLOGS
  :: Move tlogs from work folders.
  @call :MESS Begin move tlogs from work folders

  :: If there are tlogs in the work folders, move them to the Bad folder.
  if exist E:\Poll\Tlog\Tlog*.* E:\Poll\Bin\MoveFiles.exe E:\Poll\Tlog\*.* E:\Poll\Tlog\Bad
  @if errorlevel 1 @call :MESS Fatal error moving tlogs from E:\Poll\Tlog to E:\Poll\Tlog\Bad & goto ERR
  if exist E:\Poll\Tlog\Polled\Tlog*.* E:\Poll\Bin\MoveFiles.exe E:\Poll\Tlog\Polled\*.* E:\Poll\Tlog\Bad
  @if errorlevel 1 @call :MESS Fatal error moving tlogs from E:\Poll\Tlog\Polled to E:\Poll\Tlog\Bad & goto ERR

  @call :MESS End move tlogs from work folders

:CLEANFOLDERS
  :: Clean folders.
  @call :MESS Begin clean folders

  del E:\Poll\Tlog\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Tlog & goto ERR
  del E:\Poll\Tlog\Polled\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Tlog\Polled & goto ERR
  del E:\Poll\Tlog\Ends\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Tlog\Ends & goto ERR
  del E:\Poll\Upload\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Upload & goto ERR
  del E:\Poll\Outbound\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Outbound & goto ERR
  del E:\Poll\Interface\Ack\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Interface\Ack & goto ERR
  del E:\Poll\Interface\HR\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Interface\HR & goto ERR
  del E:\Poll\Interface\ShipSend\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Interface\Shipsend & goto ERR
  del E:\Poll\Interface\Ends\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Interface\Ends & goto ERR
  del E:\Poll\Interface\PollRpt\*.* /Q
  @if errorlevel 1 @call :MESS Fatal error deleting files from E:\Poll\Interface\PollRpt & goto ERR

  @call :MESS End clean folders

:CHECKWARN
  :: Check warning flag.
  @if %WARNING_FLAG%==TRUE @goto WARN

:END
  :: Program completed successfully.
  @echo.
  @echo.
  @echo Program completed successfully!
  @echo.
  @pause

  @call :MESS End Program
  @goto :EOF

:MESS
  :: Write message to log file and screen.
  @Now %1 %2 %3 %4 %5 %6 %7 %8 %9 >> %LOG_FILE%
  @ver > nul
  @echo.
  @Now %1 %2 %3 %4 %5 %6 %7 %8 %9
  @ver > nul
  @goto :EOF

:WARN
  :: Program completed with one or more non-fatal errors.
  @echo.
  @echo.
  @echo Program completed with one or more non-fatal errors.
  @echo.
  @pause

  @call :MESS End Program with non-fatal errors
  @goto ERREND

:ERR
  :: Program abended due to a fatal error.
  @color FC
  @echo.
  @echo.
  @echo FATAL ERROR! Program will be terminated.
  @echo.
  @pause

  @call :MESS Abend Program

:ERREND
  :: Append the log to the error file.
  @echo. >> %ERR_FILE%
  @echo *********************************** ERROR ************************************ >> %ERR_FILE%
  @copy %ERR_FILE% /A + %LOG_FILE% /A %ERR_FILE% /A > nul

  :: Email log.
  @echo.
  @echo. Attempting to email log to polling administrator...
  @echo.
  @blat %LOG_FILE% -subject "ERROR: PrePollingCleanup" -to christian.adams@spencergifts.com

  :: Force an error code to be returned to the O/S.
  @ren _
